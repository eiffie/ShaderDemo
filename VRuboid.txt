[bufA]
#define PI 3.14159
// hash without sine
// https://www.shadertoy.com/view/4djSRW
#define MOD3 vec3(443.8975,397.2973, 491.1871)
float hash12(vec2 p) {
	vec3 p3  = fract(vec3(p.xyx) * MOD3);
    p3 += dot(p3, p3.yzx + 19.19);
    return fract((p3.x + p3.y) * p3.z);
}
float rotm(float m){
	if(m<1.0)return 4.5;
	if(m<2.0)return 6.5; 
	if(m<3.0)return 5.5;
	if(m<4.0)return 1.5;
	if(m<5.0)return 8.5;
	if(m<6.0)return 7.5;
	if(m<7.0)return 3.5;
	if(m<8.0)return 2.5;
	return 0.5;
}
void mainImage(out vec4 fragColor, in vec2 fragCoord){
	if(fragCoord.y>1.0 || fragCoord.x>37.0)discard;
	if(iFrame==0){
		if(fragCoord.x<9.0)fragColor=vec4(1.0,0.4,0.2,1.0);
		else if(fragCoord.x<18.0)fragColor=vec4(0.2,1.0,0.4,1.0);
		else if(fragCoord.x<27.0)fragColor=vec4(0.4,0.2,1.0,1.0);
		else if(fragCoord.x<36.0)fragColor=vec4(1.0);
		else{
			fragColor=vec4(0.0,0.0,1.0,0.0);//level clicked,angle,rotating
		}
	}else{
		vec4 st=texture2D(iChannel0,vec2(36.5,0.5)/iResolution.xy);
		vec2 coord=fragCoord.xy;
		if(iMouse.z>0.0 && st.z==0.0){//rotate
			if(st.x<0.5){
				if(iMouse.y<iResolution.y*0.35)st.x=1.0;
				else if(iMouse.y<iResolution.y*0.7)st.x=2.0;
				else st.x=3.0;
			}
			st.y=clamp(1.5*PI*(iMouse.x-iMouse.z)/iResolution.x,-2.0/3.0*PI,2.0/3.0*PI);
		}else{
			if(iFrame<100){
				st.y=2.0/3.0*PI;
				if(hash12(vec2(iGlobalTime,iDate.w+float(iFrame)))<0.5)st.y*=-1.0;
				st.x=1.0+floor(2.9999*hash12(vec2(iGlobalTime-iDate.w,float(iFrame))));
			}
			if(st.y!=0.0){
				st.z=1.0;
				float a=floor(st.y*3.0/(2.0*PI)+0.5)*2.0*PI/3.0;
				st.y+=(a-st.y)*iTimeDelta*10.0;
				if(abs(a-st.y)<0.05 && abs(a)>0.05){//do rotation
					if(st.x>1.5 && fragCoord.x<27.0){//simple level rotation
						float f=mod(fragCoord.x,9.0);
						if(f<(st.x<2.5?4.0:1.0))coord.x=mod(coord.x+sign(a)*9.0,27.0);
					}else if(st.x<1.5){//trickier total rotation
						float f=floor(fragCoord.x/9.0),m=mod(fragCoord.x,9.0),r1=0.0,r2=1.0;
						if(a<0.0){
							if(f==0.0){r1=27.0;r2=0.0;}
							if(f==1.0){r1=-9.0;r2=1.0;}
							if(f==2.0)r2=2.0;
							if(f==3.0){r1=-18.0;r2=2.0;}
						}else{
							if(f==0.0){r1=27.0;r2=2.0;}
							if(f==1.0)r2=1.0;
							if(f==2.0){r1=-18.0;r2=2.0;}
							if(f==3.0){r1=-9.0;r2=2.0;}
						}
						if(r2>0.5)m=rotm(m);
						if(r2>1.5)m=rotm(m);
						coord.x=f*9.0+r1+m;
					}
					a=0.0;
				}
				if(abs(a)<0.05){
					st.xyz=vec3(0.0);//ready to rotate again
				}
			}
		}
		if(fragCoord.x>36.0){fragColor=st;}
		else fragColor=texture2D(iChannel0,coord/iResolution.xy);
	}
}
[image]
#define PI 3.14159
// hash without sine
// https://www.shadertoy.com/view/4djSRW
#define MOD3 vec3(443.8975,397.2973, 491.1871)
float hash12(vec2 p) {
	vec3 p3  = fract(vec3(p.xyx) * MOD3);
    p3 += dot(p3, p3.yzx + 19.19);
    return fract((p3.x + p3.y) * p3.z);
}
float Plane(in vec3 z, in vec4 norm){return dot(z,norm.xyz)-norm.w;}
mat3  rotAA(vec3 v, float angle){//axis angle rotation
	float c=cos(angle);vec3 s=v*sin(angle);
	return mat3(v.xxx*v,v.yyy*v,v.zzz*v)*(1.0-c)+mat3(c,-s.z,s.y,s.z,c,-s.x,-s.y,s.x,c);
}

vec3 N0=vec3(0.0,0.0,-1.0),N1,N2,U;
mat3 rmx;
float pln_y=3.0;
void setup(){
	vec4 st=texture2D(iChannel0,vec2(36.5,0.5)/iResolution.xy);
	
	float a=2.0/3.0*PI;
	mat2 mx=mat2(cos(a),sin(a),-sin(a),cos(a));
	float a2=1.0/3.0;
	mat2 mx2=mat2(cos(a2),sin(a2),-sin(a2),cos(a2));
	N0.yz=mx2*N0.yz;
	N1=N0;
	N1.xz=mx*N1.xz;
	N2=N1;
	N2.xz=mx*N2.xz;
	U=vec3(0.0,0.0,1.0);
	U.xz=mx*U.xz;

	vec3 N=vec3(0.0,1.0,0.0);
	if(st.x<1.5){
		if(st.y<0.0)N=N2;
		else N=N1;
		pln_y=3.0;
	}else if(st.x<2.5){
		pln_y=1.0-1.0/1.5;
	}else pln_y=1.0+1.0/1.5;
	rmx=rotAA(N,st.y);
}

float DE(in vec3 p){	
	vec3 ps=p;
	
	p=rmx*p;
	float d=-1.0-p.y;
	d=max(d,Plane(p,vec4(N0,1.0)));
	d=max(d,Plane(p,vec4(N1,1.0)));
	d=max(d,Plane(p,vec4(N2,1.0)));

	if(pln_y<3.0){
		d=max(d,pln_y-p.y);//abs(p.y-pln_y)-1.0/1.5);
		p=ps;
		float d2=-1.0-p.y;
		d2=max(d2,Plane(p,vec4(N0,1.0)));
		d2=max(d2,Plane(p,vec4(N1,1.0)));
		d2=max(d2,Plane(p,vec4(N2,1.0)));
		d=min(d,max(d2,p.y-pln_y));//1.0/1.5-abs(p.y-pln_y)));
	}
	return d;
}
mat3 lookat(vec3 fw, vec3 up){//normalized!!
	vec3 rt=normalize(cross(fw,up));return mat3(rt,cross(rt,fw),fw);
}
vec3 Color(in vec3 p){	
	vec3 ps=p;
	p=rmx*p;
	vec3 N=vec3(0.0,-1.0,0.0),up=vec3(0.0,1.0,0.0);
	float d1=-1.0-p.y,id=27.0;
	float d2=Plane(p,vec4(N0,1.0));
	if(d2>d1){d1=d2;N=N0;id=0.0;}
	d2=Plane(p,vec4(N1,1.0));
	if(d2>d1){d1=d2;N=N1;id=9.0;}
	d2=Plane(p,vec4(N2,1.0));
	if(d2>d1){d1=d2;N=N2;id=18.0;}
	
	if(pln_y<3.0){
		d1=max(d1,pln_y-p.y);//remove non-rotated
		vec3 NN=vec3(0.0,-1.0,0.0);
		float d3=-1.0-ps.y,id2=27.0;
		float d4=Plane(ps,vec4(N0,1.0));
		if(d4>d3){d3=d4;NN=N0;id2=0.0;}
		d4=Plane(ps,vec4(N1,1.0));
		if(d4>d3){d3=d4;NN=N1;id2=9.0;}
		d4=Plane(ps,vec4(N2,1.0));
		if(d4>d3){d3=d4;NN=N2;id2=18.0;}
		d3=max(d3,p.y-pln_y); //remove rotated
		if(d3<d1){N=NN;id=id2;p=ps;}
	}
	
	if(N==-up)up=U;
	p=p*lookat(N,up);//transform to 2d

	p.y-=0.707107;
	vec2 pt=p.xy*vec2(0.57735,1.0/3.0)*vec2(1.066,1.05);
	float f=0.24+pt.y;
	pt=pt.xx+vec2(f,-f);
	vec2 ff=floor(pt);
	
	if(p.y>0.707107){
		//id+=0.0;
	}else if(p.y>-0.707107){
		id+=3.0+ff.x+ff.y;
	}else{
		id+=7.0+ff.x+ff.y;
	}
	
	vec3 col=texture2D(iChannel0,vec2(id+0.5,0.5)/iResolution.xy).rgb;
	pt=vec2(0.5)-abs(fract(pt)-vec2(0.5));
	f=abs(fract(p.y*0.707107)-0.5);
	return col*smoothstep(0.01,0.04,min(min(pt.x,pt.y),f));
}

vec3 mytextureCube(vec3 rd){
   	rd+=sin(rd.yzx*10.0+iGlobalTime+2.4*sin(rd.zxy*9.0+iGlobalTime+1.7*sin(rd*7.0+iGlobalTime)));
    return 0.5+0.2*rd;
}

// Tetrahedral normal from IQ.
vec3 normal(vec3 p, float e){vec2 v=vec2(-e,e);return normalize(v.yxx*DE(p+v.yxx)+v.xxy*DE(p+v.xxy)+v.xyx*DE(p+v.xyx)+v.yyy*DE(p+v.yyy));}

void mainImage(out vec4 fragColor, in vec2 fragCoord){
	setup();
	vec3 ro=vec3(0.0+sin(iGlobalTime*0.3)*2.0,sin(iGlobalTime*0.4),8.0);
	vec3 rd=normalize(vec3((fragCoord-0.5*iResolution.xy)/iResolution.x,1.0));
	rd=lookat(normalize(vec3(0.0,0.75,0.0)-ro),vec3(0.0,1.0,0.0))*rd;
	float t=hash12(fragCoord)*DE(ro),d,od=1.0,g=0.0;
	for(int i=0;i<64;i++){
		t+=d=DE(ro+rd*t);
		if(d<0.001 || t>12.0)break;
		od=d;g+=1.0;
	}
	vec3 col=mytextureCube(rd).rgb+vec3(g/64.0);
	if(d<0.01){
		float dif=1.0-clamp(d/od,0.0,1.0);
		col=Color(ro+rd*t);
		col*=dif;
        vec3 N=normal(ro+rd*t,0.001);
        vec3 refl=reflect(rd,N),refr=refract(rd,N,1.0/1.5);
        vec2 IR=vec2(dot(refl,N),dot(refr,-N));
        IR=(IR-1.5*IR.yx)/(IR+1.5*IR.yx);
        float fres=dot(IR,IR)/2.0;
        col+=8.0*col*mytextureCube(refl).rgb*fres;
	}
	fragColor=vec4(col,0.0);
}
[sound]
#define PI 3.14159
#define bps 6.0
float nofs(float n){//the song's "random" ring
    n=mod(n,8.0);
    if(n<1.0)return 0.0;
    if(n<2.0)return 1.0;
    if(n<3.0)return 2.0;
    if(n<4.0)return 3.0;
    if(n<5.0)return 5.0;
    if(n<6.0)return 4.0;
    if(n<7.0)return 3.0;
    return 0.0;
}

float scale(float note){//throws out dissonant tones
	float n2=mod(note,12.0);
	//if((n2==1.0)||(n2==3.0)||(n2==6.0)||(n2==8.0)||(n2==10.0))note=-100.0;//major
	if((n2==1.0)||(n2==4.0)||(n2==6.0)||(n2==9.0)||(n2==11.0))note=-100.0;//minor
	//if((n2==1.0)||(n2==4.0)||(n2==5.0)||(n2==9.0)||(n2==10.0))note=-100.0;//hungarian minor
	//if(note>96)note=96.0+n2;
	return note;
}
float ntof(float note){//note frequencies from wikipedia
	if(note<12.0)return 0.0;
	float octave=floor((note+0.5)/12.0)-5.0;
	note=mod(note,12.0);
	float nt=493.88;//b
 	if(note<0.5)nt=261.63;//c
	else if(note<1.5)nt=277.18;//c#
	else if(note<2.5)nt=293.66;//d
  else if(note<3.5)nt=311.13;//d#
  else if(note<4.5)nt=329.63;//e
  else if(note<5.5)nt=349.23;//f
  else if(note<6.5)nt=369.99;//f#
  else if(note<7.5)nt=392.0;//g
  else if(note<8.5)nt=415.30;//g#
  else if(note<9.5)nt=440.0;//a
  else if(note<10.5)nt=466.16;//a#
	return nt*exp2(octave);
}

float Saw(float t,float s){s*=0.5;return smoothstep(0.0,s,fract(t))*smoothstep(1.0,s,fract(t))*2.0-1.0;}
float Sin(float t){return sin(t*PI);}
float Square(float t,float s){s*=0.25;return smoothstep(0.25-s,0.25+s,fract(t))*smoothstep(0.75+s,0.75-s,fract(t))*2.0-1.0;}
float Env(float t,float s){s*=0.5;return smoothstep(0.0,s,t)*smoothstep(1.0,s,t);}
float Env(float t,float s,float e){return smoothstep(0.0,s,t)*smoothstep(e,s,t);}
float rand(float t){return fract(sin(mod(t,4321.123)*4321.123)*4321.123);}
float noise(float t){float f=fract(t);t=floor(t);return mix(rand(t),rand(t+1.0),f);}
float snoise(float t){float f=fract(t);t=floor(t);return mix(rand(t),rand(t+1.0),f*f*(3.0-2.0*f));}

float tamb(float t, float o, float n){
	float bt=fract(fract(t-o)*1.5);
	float f=t*3500.0+bt*75.0*n-noise(bt*75.0*(1.0-0.9*bt))*7.0;
	float a2=Square(f,bt);
	return a2*Env(bt*(2.0+2.0*n),0.01)*Sin(1.0/(0.02+2.0*bt*bt));
}
float flute(float bt,float t,float f){
	if(f<12.0)return 0.0;
	float e=Env(bt,0.1,0.5),m=(1.0-e)*sin(t*40.0)*0.08;
	return Env(bt,0.4)*(snoise(t*1000.0)*e*0.15+m+0.5)*Square(t*f+m,0.3+bt*0.5);
}
float strings(float bt,float t,float f){
	if(f<12.0)return 0.0;
	float e=Env(bt,0.1,0.5),m=(1.0-e)*sin(t*50.0)*0.0001*f;
	return Env(bt,0.5)*(m+0.5)*Saw(t*f+m,0.1+bt*0.9);
}
vec2 mainSound(float time){
	float tim=time*bps;
	float b=floor(tim);
	float n0=nofs(b*0.0625),n1=n0+nofs(b*0.25),n2=n1+nofs(b);
	float bt=fract(tim);
	n0=scale(n0+32.0);if(n0<1.0)n0=scale(nofs(b*0.0625)+33.0);//keep base going
	float a=strings(fract(tim*0.25),time,ntof(n0))*2.0;
	a+=strings(fract(tim*0.5),time,ntof(scale(n1+72.0)))*0.3;
	a+=strings(fract(tim*0.5),time,ntof(scale(n1+75.0)))*0.1;
	a+=strings(fract(tim*0.5),time,ntof(scale(n1+79.0)))*0.2;
	a+=flute(fract(tim),time,ntof(scale(n2+60.0)));
	a+=tamb(time*3.0,0.1,rand(floor(mod(time,180.0))))*0.2;
	a=clamp(a*0.25,-1.0,1.0);
	return vec2(a);
}